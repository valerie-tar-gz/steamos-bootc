# Maintainer: Tulip Blossom (tulilirockz@proton.me)

pkgname=bootc
pkgver=1.7.1.r50.gfd703ec
pkgrel=1
pkgdesc="Boot and upgrade via container images"
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url="https://bootc-dev.github.io/bootc"
_url="https://www.github.com/$pkgname-dev/$pkgname"
license=('MIT OR Apache-2.0')
depends=(gcc-libs
  glibc
  ostree)
makedepends=(cargo
  git)
provides=("bootc=$pkgver")
conflicts=('bootc')
source=("git+$_url#branch=composefs-backend")
b2sums=('SKIP')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

pkgver() {
  cd "$pkgname"
  git describe --long --tags --abbrev=7 --match="v*" HEAD |
    sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --frozen
}

package() {
  cd "$pkgname"
  install -Dm755 target/release/bootc -t "${pkgdir}/usr/bin"
  install -Dm755 -d "${pkgdir}/usr/share/doc/$pkgname"
  cp -Rv docs/src/* "${pkgdir}/usr/share/doc/$pkgname/"
  install -Dm644 LICENSE-APACHE -t "${pkgdir}/usr/share/licenses/$pkgname/"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/$pkgname/"
}
